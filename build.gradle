plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
    id 'java'
    id 'maven-publish'
}

archivesBaseName = rootProject.archives_base_name
version = rootProject.mod_version + "+" + rootProject.minecraft_version
group = rootProject.maven_group

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.

    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url "https://maven.terraformersmc.com/releases/"
    }
    maven {
        url "https://maven.shedaniel.me"
    }
    maven { url 'https://maven.nucleoid.xyz' }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
}

loom {
    accessWidenerPath = file("src/main/resources/flashfreeze.accesswidener")
}

sourceSets {
    testmod {
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.compileClasspath
        resources {
            srcDirs += [
                    'src/testmod/generated'
            ]
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${rootProject.cca_version}"

    modImplementation("com.terraformersmc:modmenu:${rootProject.modmenu_version}") {
        transitive = false
    }

    modImplementation include("eu.pb4:polymer-core:${rootProject.polymer_version}")

    modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modCompileOnly "maven.modrinth:lithium:${rootProject.lithium_version}"

    testmodImplementation sourceSets.main.output
}

loom {
    runs {
        testmodClient {
            client()
            ideConfigGenerated project.rootProject == project
            name = "Testmod Client"
            source sourceSets.testmod
        }
        testmodServer {
            server()
            ideConfigGenerated project.rootProject == project
            name = "Testmod Server"
            source sourceSets.testmod
        }
        testModDatagen {
            client()
            ideConfigGenerated project.rootProject == project
            name = "Testmod Datagen"
            source sourceSets.testmod

            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/testmod/generated")}"

            runDir "build/datagen"
        }
    }
}


processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processTestmodResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 17
}

java {
    withSourcesJar()
}
